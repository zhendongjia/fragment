      SUBROUTINE MERGE(TAR,PRO)
C
C
C          MERGING OF COLLIDING BODIES.
C          ----------------------------
C
      INCLUDE 'COMMONP.FOR'
      INTEGER PRO, TAR
C
      WRITE (6,1) NAME(TAR), NAME(PRO), NAME(TAR)
 1    FORMAT (5X,'MERGE:',I5, I5, " -> ", I5)
C
C          FIRST EVALUATE THE OLD SOLAR INTERACTION TERMS.
      RI = DSQRT (X(1,TAR)**2 + X(2,TAR)**2 + X(3,TAR)**2)
      RJ = DSQRT (X(1,PRO)**2 + X(2,PRO)**2 + X(3,PRO)**2)
      POTS = BODY(TAR)/RI + BODY(PRO)/RJ
C
C          SET NEW VARIABLES IN ICOMP FOR THE MERGED BODY.
      BCM = BODY(TAR) + BODY(PRO)
      SPIN(TAR) = SPIN(TAR) + SPIN(PRO) + ((X(1,TAR)-X(1,PRO))
     &     *(XDOT(2,TAR) - XDOT(2,PRO)) - (X(2,TAR) - X(2,PRO))*
     &      (XDOT(1,TAR) - XDOT(1,PRO)))*BODY(TAR)*BODY(PRO)/BCM
C
      DO 5 K = 1,3
      X(K,TAR) = (BODY(TAR)*X(K,TAR) + BODY(PRO)*X(K,PRO))/BCM
      XDOT(K,TAR) = (BODY(TAR)*XDOT(K,TAR) +
     &                                    BODY(PRO)*XDOT(K,PRO))/BCM
    5 CONTINUE
C
      R(TAR) = R(TAR)*(BCM/BODY(TAR))**0.3333
      BODY(TAR) = BCM
      MPERT(TAR) = 1 + SQRT (BODY(TAR)/EMBRYO)*NBPERT
C
C          OBTAIN THE C.M. SOLAR INTERACTION TERM.
      POTCM = BODY(TAR)/DSQRT (X(1,TAR)**2 + X(2,TAR)**2 +
     &                                           X(3,TAR)**2)
C          CORRECT THE TOTAL ENERGY FOR CHANGE IN INTERACTION TERMS.
      ETOT = ETOT + POTS - POTCM
C
C          REMOVE JCOMP FROM THE PERTURBER LIST OF JCOMP OR ICOMP.
      JBIN = ILIST(TAR)
      IBIN = ILIST(PRO)
   10 NNB = LIST(1,JBIN)
      L = 1
   20 IF (L.GT.NNB)  GO TO 24
      L = L + 1
      IF (LIST(L,JBIN).NE.PRO)  GO TO 20
      IF (L.GT.NNB) GO TO 23
      DO 22 K = L,NNB
   22 LIST(K,JBIN) = LIST(K+1,JBIN)
   23 LIST(1,JBIN) = NNB - 1
      JBIN = -1
   24 IF (JBIN.EQ.-1.OR.JBIN.EQ.IBIN)  GO TO 25
      JBIN = IBIN
      GO TO 10
C
C          FIRST SEE WHETHER ANY EMBRYO TABLES SHOULD BE REMOVED.
   25 KEMB = 1
   30 IF (NEMB.EQ.0)  GO TO 60
      DO 35 I = 1,N
      IF (NAME(I).EQ.LISTE(KEMB))  GO TO 50
   35 CONTINUE
C
C          REDUCE MEMBERSHIP AND MOVE UP EMBRYO TABLES (UNLESS KEMB IS LAST).
      NEMB = NEMB - 1
      IF (KEMB.GT.NEMB)  GO TO 50
C
      DO 46 L = KEMB,NEMB
      LISTE(L) = LISTE(L+1)
      LISTC(L) = LISTC(L+1)
      NNB = LISTC(L)
      DO 44 M = 1,NNB
      DO 42 K = 1,6
   42 IEMB(K,M,L) = IEMB(K,M,L+1)
   44 CONTINUE
   46 CONTINUE
C
C          CHECK SAME LOCATION AGAIN BECAUSE OF UPDATING.
      GO TO 30
C
C          CONSIDER THE NEXT EMBRYO (IF ANY).
   50 KEMB = KEMB + 1
      IF (KEMB.LE.NEMB)  GO TO 30
C
C          SET CURRENT EMBRYO INDEX (= 0 IF NEW EMBRYO OR TOO SMALL MASS).
   60 KEMB = 0
      NAMEJ = NAME(PRO)
      IF (BODY(TAR).LT.EMBRYO)  GO TO 70
C
      DO 65 K = 1,NEMB
      IF (LISTC(K).EQ.NAME(PRO))  KEMB = K
   65 CONTINUE
C
C          INCREASE EMBRYO INDEX UNLESS IDENTIFIED ABOVE (LIMIT IS MBMAX).
      IF (KEMB.EQ.0.AND.NEMB.LT.MBMAX)  THEN
          NEMB = NEMB + 1
          KEMB = NEMB
          LISTE(KEMB) = NAME(TAR)
          LISTC(KEMB) = 0
      END IF
C
 70   IF (TAR.GT.PRO) TAR = TAR - 1
      CALL REMOVE(PRO)
C
C          OBTAIN NEW FORCE DIFFERENCES AND TIME-STEP.
      CALL FPOLY(TAR)
C
C          FORM NEW ELEMENTS WITH RESPECT TO THE SUN.
      CALL UPDATE_ORBIT(TAR)
C
      IF (KEMB.EQ.0)  GO TO 176
      IF (LISTC(KEMB).GE.NMAX)  GO TO 176
C
C          UPDATE EMBRYO VARIABLES (EVENT #, TIME, MASS, SEMI, E, EZ & NAME).
      IZ = (X(3,TAR)**2 + XDOT(3,TAR)**2)/ROCHE**2
C          INCREASE COLLISION COUNTER AND SAVE INTEGER SCALED VARIABLES.
      LISTC(KEMB) = LISTC(KEMB) + 1
      L = LISTC(KEMB)
      IEMB(1,L,KEMB) = TIME/(1000.0*TWOPI)
      IEMB(2,L,KEMB) = BCM/ZMOON
      IEMB(3,L,KEMB) = 10000.0*SEMI(TAR)
      IEMB(4,L,KEMB) = 10000.0*ECC(TAR)
      IEMB(5,L,KEMB) = 1000.0*MIN (IZ,30)
      IEMB(6,L,KEMB) = NAMEJ
C
C          CHECK OPTION FOR DIAGNOSTIC OUTPUT.
  176 IF (KZ(5).EQ.0)  GO TO 180
      CALL WRITE_OBJECT(TAR, 6, '     MERGER :')
C
  180 NSTEPN(4) = NSTEPN(4) + 1
      RETURN
C
      END
