      SUBROUTINE MERGE(ICOMP,JCOMP)
C
C
C          MERGING OF COLLIDING BODIES.
C          ----------------------------
C
      INCLUDE 'COMMONP.FOR'
C
      WRITE (6,1) NAME(ICOMP), NAME(JCOMP), NAME(ICOMP)
 1    FORMAT (5X,'MERGE:',I5, I5, " -> ", I5)
C
C          FIRST EVALUATE THE OLD SOLAR INTERACTION TERMS.
      RI = DSQRT (X(1,ICOMP)**2 + X(2,ICOMP)**2 + X(3,ICOMP)**2)
      RJ = DSQRT (X(1,JCOMP)**2 + X(2,JCOMP)**2 + X(3,JCOMP)**2)
      POTS = BODY(ICOMP)/RI + BODY(JCOMP)/RJ
C
C          SET NEW VARIABLES IN ICOMP FOR THE MERGED BODY.
      BCM = BODY(ICOMP) + BODY(JCOMP)
      SPIN(ICOMP) = SPIN(ICOMP) + SPIN(JCOMP) + ((X(1,ICOMP)-X(1,JCOMP))
     &     *(XDOT(2,ICOMP) - XDOT(2,JCOMP)) - (X(2,ICOMP) - X(2,JCOMP))*
     &      (XDOT(1,ICOMP) - XDOT(1,JCOMP)))*BODY(ICOMP)*BODY(JCOMP)/BCM
C
      DO 5 K = 1,3
      X(K,ICOMP) = (BODY(ICOMP)*X(K,ICOMP) + BODY(JCOMP)*X(K,JCOMP))/BCM
      XDOT(K,ICOMP) = (BODY(ICOMP)*XDOT(K,ICOMP) +
     &                                    BODY(JCOMP)*XDOT(K,JCOMP))/BCM
    5 CONTINUE
C
      R(ICOMP) = R(ICOMP)*(BCM/BODY(ICOMP))**0.3333
      BODY(ICOMP) = BCM
      MPERT(ICOMP) = 1 + SQRT (BODY(ICOMP)/EMBRYO)*NBPERT
      CALL UPDATE_ORBIT(ICOMP)
C
C          REMEMBER NAME OF HEAVY PERTURBER IF IT COLLIDES.
      IF (IS_JUPITER(JCOMP).EQ.1 .AND. IS_JUPITER(ICOMP).EQ.0) THEN
         IS_JUPITER(ICOMP) = 1
         NJUPITER = NJUPITER + 1
         JUPITER(NJUPITER) = ICOMP
      END IF
C
C          OBTAIN THE C.M. SOLAR INTERACTION TERM.
      POTCM = BODY(ICOMP)/DSQRT (X(1,ICOMP)**2 + X(2,ICOMP)**2 +
     &                                           X(3,ICOMP)**2)
C          CORRECT THE TOTAL ENERGY FOR CHANGE IN INTERACTION TERMS.
      ETOT = ETOT + POTS - POTCM
C
C          REMOVE JCOMP FROM THE PERTURBER LIST OF JCOMP OR ICOMP.
      JBIN = ILIST(JCOMP)
      IBIN = ILIST(ICOMP)
   10 NNB = LIST(1,JBIN)
      L = 1
   20 IF (L.GT.NNB)  GO TO 24
      L = L + 1
      IF (LIST(L,JBIN).NE.JCOMP)  GO TO 20
      IF (L.GT.NNB) GO TO 23
      DO 22 K = L,NNB
   22 LIST(K,JBIN) = LIST(K+1,JBIN)
   23 LIST(1,JBIN) = NNB - 1
      JBIN = -1
   24 IF (JBIN.EQ.-1.OR.JBIN.EQ.IBIN)  GO TO 25
      JBIN = IBIN
      GO TO 10
C
C          FIRST SEE WHETHER ANY EMBRYO TABLES SHOULD BE REMOVED.
   25 KEMB = 1
   30 IF (NEMB.EQ.0)  GO TO 60
      DO 35 I = 1,N
      IF (NAME(I).EQ.LISTE(KEMB))  GO TO 50
   35 CONTINUE
C
C          REDUCE MEMBERSHIP AND MOVE UP EMBRYO TABLES (UNLESS KEMB IS LAST).
      NEMB = NEMB - 1
      IF (KEMB.GT.NEMB)  GO TO 50
C
      DO 46 L = KEMB,NEMB
      LISTE(L) = LISTE(L+1)
      LISTC(L) = LISTC(L+1)
      NNB = LISTC(L)
      DO 44 M = 1,NNB
      DO 42 K = 1,6
   42 IEMB(K,M,L) = IEMB(K,M,L+1)
   44 CONTINUE
   46 CONTINUE
C
C          CHECK SAME LOCATION AGAIN BECAUSE OF UPDATING.
      GO TO 30
C
C          CONSIDER THE NEXT EMBRYO (IF ANY).
   50 KEMB = KEMB + 1
      IF (KEMB.LE.NEMB)  GO TO 30
C
C          SET CURRENT EMBRYO INDEX (= 0 IF NEW EMBRYO OR TOO SMALL MASS).
   60 KEMB = 0
      NAMEJ = NAME(JCOMP)
      IF (BODY(ICOMP).LT.EMBRYO)  GO TO 70
C
      DO 65 K = 1,NEMB
      IF (LISTC(K).EQ.NAME(ICOMP))  KEMB = K
   65 CONTINUE
C
C          INCREASE EMBRYO INDEX UNLESS IDENTIFIED ABOVE (LIMIT IS MBMAX).
      IF (KEMB.EQ.0.AND.NEMB.LT.MBMAX)  THEN
          NEMB = NEMB + 1
          KEMB = NEMB
          LISTE(KEMB) = NAME(ICOMP)
          LISTC(KEMB) = 0
      END IF
C
 70   IF (ICOMP.GT.JCOMP) ICOMP = ICOMP - 1
      CALL REMOVE(JCOMP)
C
C          OBTAIN NEW FORCE DIFFERENCES AND TIME-STEP.
      CALL FPOLY(ICOMP)
C
C          FORM NEW ELEMENTS WITH RESPECT TO THE SUN.
      CALL UPDATE_ORBIT(ICOMP)
C
      IF (KEMB.EQ.0)  GO TO 176
      IF (LISTC(KEMB).GE.NMAX)  GO TO 176
C
C          UPDATE EMBRYO VARIABLES (EVENT #, TIME, MASS, SEMI, E, EZ & NAME).
      IZ = (X(3,ICOMP)**2 + XDOT(3,ICOMP)**2)/ROCHE**2
C          INCREASE COLLISION COUNTER AND SAVE INTEGER SCALED VARIABLES.
      LISTC(KEMB) = LISTC(KEMB) + 1
      L = LISTC(KEMB)
      IEMB(1,L,KEMB) = TIME/(1000.0*TWOPI)
      IEMB(2,L,KEMB) = BCM/ZMOON
      IEMB(3,L,KEMB) = 10000.0*SEMI(ICOMP)
      IEMB(4,L,KEMB) = 10000.0*ECC(ICOMP)
      IEMB(5,L,KEMB) = 1000.0*MIN (IZ,30)
      IEMB(6,L,KEMB) = NAMEJ
C
C          CHECK OPTION FOR DIAGNOSTIC OUTPUT.
  176 IF (KZ(5).EQ.0)  GO TO 180
      CALL WRITE_OBJECT(ICOMP, 6, '     MERGER :')
C
  180 NSTEPN(4) = NSTEPN(4) + 1
      RETURN
C
      END
